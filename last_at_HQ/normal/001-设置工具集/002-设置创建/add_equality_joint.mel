/*
    Script:     add_equality_joint.mel
    Version:    1.0
    Author:     Jason Schleifer
    Website:    http://jonhandhisdog.com

    Descr:      Splits the selected joint into the specified number of segments.
    备注：添加作者：lsy（刘升耀）
         说明：新添加了 可以在笔直的曲线上添加相等骨骼 弯曲的曲线只能得到近值相等骨骼

*/
global proc js_splitSelJoint (int $numSegments)
{
    // This proc will split the selected joint into the specified number of segments    //
    //

    if ($numSegments < 2)
    {
        error ("The number of segments has to be greater than 1.. ");
    }
    // VARIABLES
    //
    string $joints[0];
    string $joint;
    
    string $newChildJoints[0];
    int $count = 0;
    $joints = `ls -sl -type joint`;
    for ($joint in $joints)
    {
        // first check the rotation order vs. the axis the joint should be rotating around.  In order for this to work
        // correctly, the child joint of the joint specified must have translation values ONLY IN ONE AXIS.  Let's
        // and, that axis MUST be the first one in the rotation order.  For example, if the joint structure is:
        //      upArm->loArm
        // and the loArm has a translate value of 6 0 0, then upArm should have a rotation order of XYZ or XZY.
        //
        string $child;

        $child = `js_getChildJoint $joint`;

        if (size($child) == "")
        {
            error ("Joint: " + $joint + " has no children joints.\n");
        }
        else
        {
            string $axis;
            string $rotationOrder;
            string $firstChar;
            float $radius = `getAttr ($joint + ".radius")`;

            $axis = `js_getJointAxis $child`;
            
            // now that we have the axis, we want to check and make sure the rotation order on $joint is correct.
            int $rotOrderIndex = `getAttr ($joint + ".rotateOrder")`;
            $rotationOrder = `js_getRotOrderString $joint`;

            // ** REMOVING CHECK FOR ROTATION ORDER MATCHING
            // check that the rotaiton order will work.
            // 
            //$firstChar = `substring $rotationOrder 1 1`;
            //if ($axis == $firstChar)
            //{
                float $childT = 0.0;
                float $tVal = 0.0;
                string $attr;

                // the rotation order is correct!  we're set!

                // get the axis attr
                $attr = ("t"+$axis);

                // get the value of the child
                $childT = `getAttr ($child + "." + $attr)`;
                float $space = $childT/$numSegments; 
                // create a series of locators along the joint based on the number of segments.
                string $locators[0];

                for ($x = 0; $x < ($numSegments-1); $x++)
                {
                   $tmp = `spaceLocator`; 
                   $locators[$x] = $tmp[0];

                    parent $locators[$x] $joint;
                    setAttr ($locators[$x] + ".t") 0 0 0;
                    setAttr ($locators[$x] + "." + $attr) ($space * ($x+1));
                }
               
                // We just want to segment the joint, nothing more.
            
                // for each segment, we're going to insert a joint, and then move it to the position of the locator
                string $prevJoint = $joint;
                for ($x = 0; $x < size($locators); $x++)
                {
                    // insert a joint
                    $newJoint = `insertJoint $prevJoint`;

                    // get the position of the locator
                    $pos = `xform -q -ws -rp $locators[$x]`;

                    // move the joint there
                    move -a -ws $pos[0] $pos[1] $pos[2] ($newJoint + ".scalePivot") ($newJoint + ".rotatePivot");
                    
                    // rename the new joint
                    $newJoint = `rename ($newJoint) ($joint + "_seg_"+($x+1)+"_joint")`;
                    
                    setAttr ($newJoint + ".radius") $radius;

                    // set the rotation order
                    setAttr ($newJoint + ".rotateOrder") $rotOrderIndex;
                    // set the prevJoint
                    $prevJoint = $newJoint;
                }

                delete $locators;
            // ** END ROTATION ORDER CHECK
            //}
            //else
            //{
                // The rotation order is incorrect.  The user needs to re-orient the joint
                //js_orientJointWarning $joint $rotationOrder $axis $firstChar;

            //}
        }
    }

}
global proc js_orientJointWarning (string $joint, string $rotationOrder, string $axis, string $firstChar )
{
    string $message;
    $message = ("Warning!!!!\n\n");
    $message += ("The rotation order and joint orient for "  + $joint + " do not match up.\n");
    $message += ("The rotation order is: " + $rotationOrder + "\n");
    $message += ("The axis aiming down the joint is: " + $axis + "\n\n");
    $message += ("In order for this script to work, you need to either change the axis that\n");
    $message += ("aims down the joint to " + $firstChar + ", or switch the rotation order to ");
    switch ($axis)
    {
        case "x":
            $message += ("xyz or xzy.\n");
            break;
        case "y":
            $message += ("yxz or yzx.\n");
            break;
        case "z":
            $message += ("zxy or zyx.\n");
            break;
    }
    $message += ("\nSkipping Joint: " + $joint + "...\n");
    confirmDialog -m $message -ma "left";
    warning $message;

}
global proc string js_getRotOrderString (string $joint)
{
    string $return;
    int $ro;
    $ro = `getAttr ($joint + ".ro")`;

    switch ($ro)
    {
        case 0:
            $return = "xyz";
            break;
        case 1:
            $return = "yzx";
            break;
        case 2:
            $return = "zxy";
            break;
        case 3:
            $return = "xzy";
            break;
        case 4:
            $return = "yxz";
            break;
        case 5:
            $return = "zyx";
            break;
    }
    return $return;
}
global proc string js_getJointAxis (string $child)
{
    string $axis;
    float $t[0];
    $t = `getAttr ($child + ".t")`;

    // get the translation values of the $child joint
    // now check and see which one is greater than 0.  We should have a tolerance value just in case
    float $tol = 0.0001;
    for ($x = 0; $x <= 2; $x++)
    {
        if (($t[$x] > $tol) || ($t[$x] < (-1 *$tol)))
        {
            switch ($x)
            {
                case 0:
                    $axis = "x";
                    break;
                case 1:
                    $axis = "y";
                    break;
                case 2:
                    $axis = "z";
                    break;
            }
        }
    }

    if ($axis == "")
    {
        error ("The child joint is too close to the parent joint. Cannot determine the proper axis to segment.");
    }
    return $axis;
}
global proc string js_getChildJoint (string $joint)
{
    string $tmp[0];
    $tmp = `listRelatives -f -c -type joint $joint`;
    return ($tmp[0]);
}





/*
    Script:     js_getOptionVar.mel
    Version:    1.0
    Author:     Jason Schleifer
    Website:    http://jonhandhisdog.com

    Descr:      Used to check if an optionVar exists.  If it does, it returns the value.

*/
global proc int js_getOptionVar (int $default, string $optionVar)
{
    // check and see if the optionVar exists
    int $return = $default;
    if (!`optionVar -exists $optionVar`)
    {
        optionVar -iv $optionVar $default;
    }
    $return = `optionVar -q $optionVar`;

    return $return;
}




/*
    Script:     js_splitSelJointUI.mel
    Version:    1.0
    Author:     Jason Schleifer
    Website:    http://jonhandhisdog.com

    Descr:      Interface to js_splitSelJoint.mel

    Req:        js_splitSelJoint.mel
                js_getOptionVar.mel

*/
global proc js_splitSelJointUI ()
{
    // Builds the interface for the splitSelJointUI

    string $win  = "js_splitSelJointWin";
    if (`window -exists $win`)
        deleteUI $win;

    window -t "Split Selected Joints" $win;

    $f = `formLayout -nd 100`;

    $segments = `intSliderGrp -field true -l "Segments" -min 2 -max 100 js_intSliderGrp1`;
    $b1 = `button -l "Okay" js_button_1`;
    $b2 = `button -l "Cancel"`;
    formLayout -e

        -af $segments top 5
        -af $segments left 5
        -af $segments right 5

        -af $b1 left 5
        -af $b1 bottom 5
        -ap $b1 right 0 47

        -af $b2 right 5
        -af $b2 bottom 5
        -ap $b2 left 0 52
        $f;

    // set up callbacks
    button -e -c ("deleteUI \"" + $win + "\"") $b2;
    button -e -c "ls_jointOrCurve" $b1 ;
    // set up defaults
    int $segmentOpt = 2;
    
    $segmentOpt = `js_getOptionVar $segmentOpt "js_splitSelSegments"`;

    // now set the item
    intSliderGrp -e -value $segmentOpt $segments;

    showWindow $win;
}

global proc js_buildSplitJointCmd (string $segments)
{
    // get the values
    $segmentVal = `intSliderGrp -q -value $segments`;

    // set the optionVars
    optionVar -iv "js_splitSelSegments" $segmentVal;

    // build the command
    $cmd = ("js_splitSelJoint " + $segmentVal);
    evalEcho $cmd;
}

global proc ls_jointOrCurve(){
	
	string $ls_one;
	string $ls_jOrC[]=`ls -sl`;
	int $ls=`size($ls_jOrC)`;
	if($ls==0){
		
		print ("请选择joint或者curve执行"+"\n");
	};
	
	for($ls_one in $ls_jOrC){
		
		
		select -r $ls_one;
		string $se_jnt_n[]=`ls  -sl  -showType`;
		
		if($se_jnt_n[1] =="joint"){
			
			select -r $ls_one;
			eval("js_buildSplitJointCmd \"" + "js_intSliderGrp1" + "\"");			 
		}else{
			
			select -r $ls_one;
			string $se_ls[]=`listRelatives -shapes`;
			select $se_ls;
			string $se_ls_n[]=`ls  -sl  -showType`;
			
			if($se_ls_n[1]=="nurbsCurve"){
				
				select -r $ls_one;
				eval("jointOnCurve");
			}else{
				
				print ("请选择joint或者curve执行"+"\n");
				
			}
			
				
		};
		


    }
}






global proc jointOnCurve(){
	
	string $one_c;
	string $all_curve[]=`ls -sl`;
	int $de_cv_joint=0;
	int $segments = `intSliderGrp -q -value  js_intSliderGrp1`;
	int $nun_input=$segments-1;
	int $nun_int=$nun_input*20;
	
	for($one_c in $all_curve){
		
		select -r $one_c;
		string $one_copy=$one_c+"_copy";
		duplicate -rr -n $one_copy;
		select -r $one_copy;
		DeleteHistory;
		rebuildCurve -ch 1 -rpo 1 -rt 0 -end 1 -kr 0 -kcp 0 -kep 1 -kt 0 -s $nun_int -d 3 -tol 0.01 $one_copy;
		print $one_copy;
		
		delete ($one_copy+".cv[1]");
		delete ($one_copy+".cv["+$nun_int+"]");
		select -r $one_copy;
		selectCurveCV("last");
		string $se_cv[]=`ls -sl`;
		string $cv_i1=`match "[+[0-9]+]+" $se_cv[0]`;
		string $cv_i2=`match "[0-9]+" $cv_i1`;
		int $cv_i3= $cv_i2;
		int $cv_i=$cv_i3+1;
		
		select -r $one_copy;
		arclen -ch true;
		string $one_n=$one_copy+"_Info1";
		rename "curveInfo1" $one_n;
		select -cl;
		
		int $joint_i=1;
		
		
		for($i=0;$i<$cv_i;$i=$i+20){
			
			string $one_n_xV=$one_n+".controlPoints["+$i+"].xValue";
			string $one_n_yV=$one_n+".controlPoints["+$i+"].yValue";
			string $one_n_zV=$one_n+".controlPoints["+$i+"].zValue";
			
			float $one_n_xV_int = `getAttr $one_n_xV`;
			float $one_n_yV_int = `getAttr $one_n_yV`;
			float $one_n_zV_int = `getAttr $one_n_zV`;
			
			string $one_joint=$one_c+"_joint"+$joint_i;
			$joint_i = $joint_i+1;
			string $one_joint_tx=$one_joint+".tx";
			string $one_joint_ty=$one_joint+".ty";
			string $one_joint_tz=$one_joint+".tz";
			
			if($i==0){
				
				joint -p $one_n_xV_int $one_n_yV_int $one_n_zV_int -n $one_joint;
				
			}else{
				
				int $up_joint_i=$joint_i-2;
				string $up_joint=$one_c+"_joint"+$up_joint_i;
				joint -p $one_n_xV_int $one_n_yV_int $one_n_zV_int -n $one_joint;
				joint -e -zso -oj xyz -sao yup $up_joint;
				
			}
			
		 }
		 delete $one_n;
		 delete $one_copy;
		
	}
	
}



js_splitSelJointUI;