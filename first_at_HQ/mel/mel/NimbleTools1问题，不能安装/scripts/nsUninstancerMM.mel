/* This file downloaded from Highend3d.com
''  
''  Highend3d.com File Information:
''  
''    Script Name: Uninstancer
''    Author:  
''    Last Updated: Jan 15, 2009
''    Update/Change this file at:
''    http://Highend3d.com/maya/downloads/plugins/dynamics/4331.html
''  
''  Please do not alter any information above this line
''  it is generated dynamically by Highend3d.com and will
''  be changed automatically on any updates.
*/
global proc nsUninstancerMM( string $parent, string $object )
{
	if ( `pluginInfo -q -loaded NimbleTools` )
	{
		string $shape =  nsGetSelectedShape( $object, "particle" );
		// Only show the menu if the user is over a particle.
		//
		if ( "" != $shape )
		{
			string $instancers[] = `listConnections ($shape + ".instanceData")`;

			// Only show the Uninstance menu if there are instancers to
			// uninstance.
			//
			if ( `size $instancers` > 0 )
			{
				menuItem
					-l "Uninstance"
					-ann "Uninstance: Bake particle instancer geometry/animation"
					-allowOptionBoxes true
					-subMenu true
					nsUninstanceMenuItemMM;

				menuItem
					-l "All"
					-ann "Uninstance all associated particle instancers"
					-c ("nsUninstanceAllOnParticle( \"" + $shape + "\" )")
					nsUninstanceAllInstancersMM;
				menuItem -divider true;
				
				string $instancer;
				for ( $instancer in $instancers )
				{
					menuItem
						-l $instancer
						-ann ("Uninstance " + $instancer)
						-c ("nsPerformUninstancer( 0, \"" + $instancer + "\" )")
						("nsUninstance" + $instancer + "MM");
					menuItem
						-optionBox true
						-l "Uninstance Option Box"
						-ann "Uninstance Option Box"
						-c ("nsPerformUninstancer( 1, \""+ $instancer + "\" )")
						("nsUninstance" + $instancer + "OptionsMM");
				}

				setParent -m ..;
				menuItem -divider true;
				setParent -m $parent;
			}
		}
	}
}